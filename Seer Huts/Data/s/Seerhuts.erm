ZVSE

!?CM0; Right-click on adventure map
!!CM:I?y1;
!!FU&y1<>5:E; exit if not click on quest log
!!VRz1:S^^;
!!VRz-1:S^PICK YOUR CHOICE^;
!!VRz2:S^Options^;
!!VRz3:S^..\Data\srh.bmp^;
!!VRz4:S^..\Data\quest.bmp^;
!!VRz5:S^..\Data\keys.bmp^;
!!VRz6:S^^;
!!VRz7:S^LOCATE SEER HUTS^;
!!VRz8:S^LOCATE QUEST GUARDS^;
!!VRz9:S^LOCATE KEYMASTER TENTS^;
!!VRz10:S^^; *
!!VRz11:S^Will show all seer huts and what they ask^;
!!VRz12:S^Will show all quest guards and what they ask^;
!!VRz13:S^Will show all keymaster tents and their border guards^;
!!VRz14:S^^;
!!VRz15:S^^;
!!VRz16:S^^;
!!VRz17:S^^;
!!VRz18:S^^;
!!IF:D97/-1/1/2/3/4/5/6/15/16/17/18/7/8/9/10;
!!IF:F97/11/12/13/14/0;
!!IF:E2/97;
!!CM:R0;
!!if&v2=3:; tents
!!DL212:N^seerhuts.txt^;
!!DL212:S1;
!!en:;
!!if&v2=1:; open Seers
!!DL209:N^seerhuts.txt^;
!!UN:U83/-1/?y-1;  find seer huts
!!VRy-1&y-1>48:S48;
!!VRv6:S-1;
!!DO32165490/1/y-1/1&y-1>0:P83/49/97/y-1/1;
!!DL209:S1;
!!en:;

!!if&v2=2:; open Border Guards
!!DL211:N^seerhuts.txt^;
!!UN:U215/-1/?y-1;  find seer huts
!!VRy-1&y-1>48:S48;
!!VRv1:S-1;
!!DO32165496/1/y-1/1&y-1>0:P215/1;
!!DL211:S1;
!!en:;

!?FU32165496; loop border Guards
!!UN:Ux1/-1/-1/1; seer hut in v1-v3
!!OW:C?y7;
!!VRy8&y7=0:S1; !!VRy8&y7=1:S2; !!VRy8&y7=2:S4; !!VRy8&y7=3:S8;
!!VRy8&y7=4:S16; !!VRy8&y7=5:S32; !!VRy8&y7=6:S64; !!VRy8&y7=7:S128; convert to player bits
!!TR1:V?i; get square visibility for current player
!!VRi:&y8;
!!DL211&i<>y8:Ax2/4/1/1; set border guard frame active
!!DL211&i=y8:Ax2/4/2/1; set border guard frame active
!!SN:W^x_bcoord_%X2^/v1; store coordinates
!!SN:W^y_bcoord_%X2^/v2;
!!SN:W^z_bcoord_%X2^/v3;
!!VRx2:+1; increment +1 for next search


!?DL&v998=211/v999>0/v999<49/v1000=13; border guards click
!!VRz1:S^Button.wav^;
!!SN:Pz1;
!!SN:W^border_clicked^/v999;
!!VRy1:Sv999; get border guard location
!!SN:W^x_bcoord_%Y1^/?v1;
!!SN:W^y_bcoord_%Y1^/?v2;
!!SN:W^z_bcoord_%Y1^/?v3;
!!FU&v1<1/v2<1:E; exit if no quest
!!QU1:Q?y3; check quest type
!!DO32165497/49/96/1:P; reset extra infos frame
!!VRy2:Sv999+48;
!!DL211:Ay2/4/y3/1; set frame for extra infos
!!if&y3<>6:; all except bring monsters
!!VRz2&y3=0:S^^;
!!QU1&y3>0:M0/?z2;
!!DL211:A501/3/z2/1; set description text
!!en:;
!!if&y3=6:;   bring monsters
!!VRv9:C-1/-1; initialize v9-v10
!!DO32165498/0/196/1:Pv1/v2/v3;
!!UN&v10>1:N3/z3/v9/1;
!!UN&v10<2:N3/z3/v9/0;
!!VRz2:S^Bring %V10 %Z3^;
!!DL211:A501/3/z2/1;
!!en:;

!?FU32165498;
!!QUx1/x2/x3:Q6/x16/?y1;
!!FU&y1<1:E;
!!VRv9:Sx16; !!VRv10:Sy1;
!!VRx16:S999;

!?FU32165497;  reset extra infos frame
!!DL211:Ax16/4/0/1;

!?FU32165490;
!!UN:Ux1/-1/-1/6; seer hut in v6-v8
!!OB6:T?y97 U?y98;
!!UN&y97=34:P904/1 P905/0; clear error flag & turn errors off
!!QU6:N?y10; get seer hut ID (0-47)
!!VRy10:+1; set to ini file
!!SN:L^era.dll^/?y1 Ay1/^ReadStrFromIni^/?y2; read from ini file
!!SN:Ey2/1/^%Y10^/^Player Settings^/^SeerHut.ini^/?z1;
!!QU6:Q?y3; check quest type
!!VRz1&y3=0:S^{~SaddleBrown}Completed{~}^;
!!OW:C?y7;
!!VRy8&y7=0:S1; !!VRy8&y7=1:S2; !!VRy8&y7=2:S4; !!VRy8&y7=3:S8;
!!VRy8&y7=4:S16; !!VRy8&y7=5:S32; !!VRy8&y7=6:S64; !!VRy8&y7=7:S128; convert to player bits
!!TR6:V?i; get square visibility for current player
!!VRi:&y8;
!!VRz2&i<>y8:S^{~Peru}%Z1{~}^;
!!VRz2&i=y8:S^{~PaleGoldenRod}%Z1{~}^;
!!DL209:Ax2/3/z2/1; set seer name
!!DL209:Ax5/4/y98/1; set graphics
!!SN:W^x_coord_%X2^/v6;
!!SN:W^y_coord_%X2^/v7;
!!SN:W^z_coord_%X2^/v8;
!!VRx2:+1;
!!VRx5&x5<x4:+1;
!!if&x16=x4:;
!!VRv6:C-1/-1/-1;
!!en:;

!?DL&v998=209/v999>48/v999<97/v1000=13; seer huts name click
!!SN:W^seer_clicked^/v999;
!!VRy1:Sv999; get seer hut location
!!SN:W^x_coord_%Y1^/?v6;
!!SN:W^y_coord_%Y1^/?v7;
!!SN:W^z_coord_%Y1^/?v8;
!!FU&v6<1/v7<1:E;
!!VRz1:S^Button.wav^;
!!QU6:Q?y3; check quest type
!!SN&y3>0:Pz1;
!!DO32165491/97/144/1:P;
!!VRy2:Sv999+48;
!!DL209:Ay2/4/y3/1; set frame
!!if&y3<>6:;
!!VRz2&y3=0:S^^;
!!QU6&y3>0:M0/?z2;
!!DL209:A401/3/z2/1;
!!en:;
!!if&y3=6:;
!!VRv9:C-1/-1; initialize v9-v10
!!DO32165495/0/196/1:Pv6/v7/v8;
!!UN&v10>1:N3/z3/v9/1;
!!UN&v10<2:N3/z3/v9/0;
!!VRz2:S^Bring %V10 %Z3^;
!!DL209:A401/3/z2/1;
!!en:;


!?FU32165491;
!!DL209:Ax16/4/0/1; reset frames for quests ID

!?DL&v998=209/v999=30722/v1000=13;
!!DL209:C1; close
!!UN:P904/0; turn on errors showing
!!IF:Q1^Move view on last seer selected?^;
!!CM&-1:R0;
!!FU&-1:E;
!!OW:C?y1; get player
!!UN&v6>-1/v7>-1:Sv6/v7/v8/y1/1;
!!UN&v6>-1/v7>-1:Lv6/v7/v8/300; move view
!!CM:R0;

!?DL&v998=209/v999>96/v999<145/v1000=14; right-click
!!SN:W^seer_clicked^/?y2;
!!VRy1:Sv999-48;
!!FU&y1<>y2:E;
!!VRy1:Sv999-48; get seer hut location
!!SN:W^x_coord_%Y1^/?v6;
!!SN:W^y_coord_%Y1^/?v7;
!!SN:W^z_coord_%Y1^/?v8;
!!FU&v6<1/v7<1:E;
!!QU6:Q?y1;
!!FU&y1<1:E;
  !!if&y1=1:; achieve hero level
  !!QU6:Q1/?y2; check for level asked
  !!IF:Q1/17/d/4^Level required: %Y2^;
  !!en:;
      !!if&y1=2:; achieve primary skills
      !!QU6:Q2/?y2/?y3/?y4/?y5;
      !!IF:Q1/31/y2/32/y3/33/y4/4^
      Requires:^;
      !!en:;
         !!if&y1=3:; kill an hero
         !!QU6:Q3/?y2/?y3; y2=hero ID
         !!HEy2:B0/?z4; get hero name
         !!IF:Q1^Kill %Z4.

         Do you want to locate %Z4?^;
         !!FU&-1:E;
         !!DO32165492/0/155/1:Py2;
         !!SN:W^Seer_hero^/?y4;
         !!FU&y4<99:E;
         !!OW:C?y3; check player
         !!DL209:C1; close dialog
         !!UN:Sv3/v4/v5/y3/2;
         !!UN:Lv3/v4/v5/200;
         !!SN:W^Seer_hero^/0;
         !!en:;
             !!if&y1=4:; kill a creature
             !!QU6:Q4/?y2/?y3/?y4/?y5/?y6; get coordinates in y2-y4, creature ID in y5
             !!UN:N3/z3/y5/1; get name at plural
             !!IF:Q1/21/y5/2^Kill the %Z3 at coordinates %Y2/%Y3/%Y4.

             Want to move view on the creatures to kill?^;
             !!FU&-1:E;
             !!OW:C?y7; get player
             !!DL209:C1;
             !!UN:Sy2/y3/y4/y7/2; open view
             !!UN:Ly2/y3/y4/100; move view
             !!en:;
                 !!if&y1=5:; bring artifacts
                 !!VRv9:C-1/-1/-1;
                 !!VRz4:S^^; !!VRz5:S^^;
                 !!DO32165493/7/170/1:Pv6/v7/v8/9;
                 !!UN:N0/z3/v9; get name
                 !!UN&v10>-1:N0/z4/v10;
                 !!UN&v11>-1:N0/z5/v11;
                 !!IF&v10<0:Q1/8/v9/4^Bring %Z3^;
                 !!IF&v10>-1/v11<0:Q1/8/v9/8/v10/4^Bring %Z3 and %Z4^;
                 !!IF&v11>-1:Q1/8/v9/8/v10/8/v11/4^Bring %Z3, %Z4 and %Z5^;
                 !!en:;
                    !!if&y1=6:; bring creatures
                    !!VRv9:C-1/-1/-1; initialize v9-v11
                    !!DO32165495/0/196/1:Pv6/v7/v8;
                    !!UN&v10>1:N3/z3/v9/1;
                    !!UN&v10<2:N3/z3/v9/0;
                    !!IF:Q1/21/v9/4^Bring %V10 %Z3^;
                    !!en:;
                        !!if&y1=7:; bring resources
                        !!VRv9:C-1/-1/-1/-1/-1/-1;
                        !!DO32165494/0/6/1:Pv6/v7/v8/9/10;
                        !!IF&v11<0:Q1/v9/v10/4^Bring:^;
                        !!IF&v11>-1/v13<0:Q1/v9/v10/v11/v12/4^Bring:^;
                        !!IF&v13>-1:Q1/v9/v10/v11/v12/v13/v14/4^Bring:^;
                        !!en:;
                           !!if&y1=8:; visit with hero
                           !!QU6:Q8/?y2; hero id y2
                           !!HEy2:B0/?z2;
                           !!IF:Q1/-1/-1/4^Send %Z2^;
                           !!en:;
                               !!if&y1=9:; visit with certain player
                               !!QU6:Q9/?y2;
                               !!IF:Q1/10/y2/4^Send player:^;
                               !!en:;


!?FU32165492;
!!FU&x16<>x1:E;
!!HEx16:O?y1;
!!IF&y1<0:M^%Z4 doesn't belong to any active player!^;
!!FU&y1<0:E;
!!SN:W^Seer_hero^/99;
!!HEx16:P?v3/?v4/?v5;

!?FU32165493;
!!QUx1/x2/x3:Q5/x16/?y1;
!!FU&y1<1:E;
!!VRvx4:Sx16;
!!VRx4:+1;

!?FU32165494;
!!QUx1/x2/x3:Q7/x16/?y1;
!!FU&y1<1:E;
!!VRvx4:Sx16; !!VRvx5:Sy1;
!!VRx4:+2;
!!VRx5:+2;

!?FU32165495;
!!QUx1/x2/x3:Q6/x16/?y1;
!!FU&y1<1:E;
!!VRv9:Sx16; !!VRv10:Sy1;
!!VRx16:S999;

;quest guards

!?DL&v998=211/v999=30722/v1000=13; close dialog
!!DL211:C1; close
!!IF:Q1^Move view on last Quest Guard selected?^;
!!CM&-1:R0;
!!FU&-1:E;
!!OW:C?y1; get player
!!UN&v1>0/v2>0:Sv1/v2/v3/y1/1;
!!UN&v1>0/v2>0:Lv1/v2/v3/300; move view
!!CM:R0;

!?DL&v998=211/v999>48/v999<97/v1000=14; right-click on extra infos frames
!!SN:W^border_clicked^/?y2;
!!VRy1:Sv999-48; find seer hut location
!!FU&y1<>y2:E;
!!SN:W^x_bcoord_%Y1^/?v1;
!!SN:W^y_bcoord_%Y1^/?v2;
!!SN:W^z_bcoord_%Y1^/?v3;
!!FU&v1<1/v2<1:E;
!!QU1:Q?y1;
!!FU&y1=0:E; exit if no quest
  !!if&y1=1:; achieve hero level
  !!QU1:Q1/?y2; check for level asked
  !!IF:Q1/17/d/4^Level required: %Y2^;
  !!en:;
      !!if&y1=2:; achieve primary skills
      !!QU1:Q2/?y2/?y3/?y4/?y5;
      !!IF:Q1/31/y2/32/y3/33/y4/4^
      Requires:

      %Y2 attack
      %Y3 defense
      %Y4 spell power
      %Y5 knowledge^;
      !!en:;
         !!if&y1=3:; kill an hero
         !!QU1:Q3/?y2/?y3; y2=hero ID
         !!HEy2:B0/?z4; get hero name
         !!IF:Q1^Kill %Z4.

         Do you want to locate %Z4?^;
         !!FU&-1:E;
         !!DO32165492/0/155/1:Py2;
         !!SN:W^Seer_hero^/?y4;
         !!FU&y4<99:E;
         !!OW:C?y3; check player
         !!DL211:C1; close dialog
         !!UN:Sv3/v4/v5/y3/2;
         !!UN:Lv3/v4/v5/200;
         !!SN:W^Seer_hero^/0;
         !!en:;
             !!if&y1=4:; kill a creature
             !!QU1:Q4/?y2/?y3/?y4/?y5/?y6; get coordinates in y2-y4, creature ID in y5
             !!UN:N3/z3/y5/1; get name at plural
             !!IF:Q1/21/y5/2^Kill the %Z3 at coordinates %Y2/%Y3/%Y4.

             Want to move view on the creatures to kill?^;
             !!FU&-1:E;
             !!OW:C?y7; get player
             !!DL211:C1;
             !!UN:Sy2/y3/y4/y7/2; open view
             !!UN:Ly2/y3/y4/100; move view
             !!en:;
                 !!if&y1=5:; bring artifacts
                 !!VRv9:C-1/-1/-1;
                 !!VRz4:S^^; !!VRz5:S^^;
                 !!DO32165493/7/170/1:Pv1/v2/v3/9;
                 !!UN:N0/z3/v9; get name
                 !!UN&v10>-1:N0/z4/v10;
                 !!UN&v11>-1:N0/z5/v11;
                 !!IF&v10<0:Q1/8/v9/4^Bring %Z3^;
                 !!IF&v10>-1/v11<0:Q1/8/v9/8/v10/4^Bring %Z3 and %Z4^;
                 !!IF&v11>-1:Q1/8/v9/8/v10/8/v11/4^Bring %Z3, %Z4 and %Z5^;
                 !!en:;
                    !!if&y1=6:; bring creatures
                    !!VRv9:C-1/-1; initialize v9-v10
                    !!DO32165495/0/196/1:Pv1/v2/v3;
                    !!UN&v10>1:N3/z3/v9/1;
                    !!UN&v10<2:N3/z3/v9/0;
                    !!IF:Q1/21/v9/4^Bring %V10 %Z3^;
                    !!en:;
                        !!if&y1=7:; bring resources
                        !!VRv9:C-1/-1/-1/-1/-1/-1;
                        !!DO32165494/0/6/1:Pv1/v2/v3/9/10;
                        !!IF&v11<0:Q1/v9/v10/4^Bring:^;
                        !!IF&v11>-1/v13<0:Q1/v9/v10/v11/v12/4^Bring:^;
                        !!IF&v13>-1:Q1/v9/v10/v11/v12/v13/v14/4^Bring:^;
                        !!en:;
                           !!if&y1=8:; visit with hero
                           !!QU1:Q8/?y2; hero id y2
                           !!HEy2:B0/?z2;
                           !!IF:Q1/-1/-1/4^Send %Z2^;
                           !!en:;
                               !!if&y1=9:; visit with certain player
                               !!QU1:Q9/?y2;
                               !!IF:Q1/10/y2/4^Send player:^;
                               !!en:;

;Keymaster Tents
!?DL&v998=212/v999=30722/v1000=13;
!!DL212:C1;

!?DL&v998=212/v999>8/v999<27/v1000=13;
!!VRy1:Sv999-9; get tent subtype
!!VRz1&y1=0:S^Light-Blue^; !!VRz1&y1=1:S^Green^; !!VRz1&y1=2:S^Red^; !!VRz1&y1=3:S^Dark-Blue^; !!VRz1&y1=4:S^Brown^;
!!VRz1&y1=5:S^Purple^; !!VRz1&y1=6:S^White^; !!VRz1&y1=7:S^Black^;
!!if&v999<17:; if tents button
!!UN:U10/y1/?y-1;  find that tent
!!IF&y-1<1:M^There are not any %Z1 tents!^;
!!FU&y-1<1:E; exit if no tents
!!en:;
!!if&v999>16:; if border gates or border guards
!!VRy2:Sv999-19;
!!UN:U212/y2/?y-1;  find border gate
!!UN:U9/y2/?y-2;    find border guard
!!IF&y-1<1/y-2<1:M^There are not any %Z1 border gates or guards!^;
!!FU&y-1<1/y-2<1:E; exit if no tents or borders
!!en:;
!!DL212:C1;
!!OW:C?y3;
!!VRv1:S-1;
!!if&v999<17:;
!!IF&y-1>0:M^Found %Y-1 tents^;
!!DO32165499/1/y-1/1&y-1>0:P10/y1/y3/y-1;
!!en:;
!!if&v999>16:; if borders
!!UN:U212/y2/?y-1;  find border gate
!!VRv1:S-1;
!!VRz2&y-1<2:S^gate^;
!!VRz2&y-1>1:S^gates^;
!!IF&y-1>0:M^Found %Y-1 border %Z2^;
!!DO32165500/1/y-1/1&y-1>0:P212/y2/y3/y-1;
!!UN:U9/y2/?y-2;    find border guard
!!VRv1:S-1;
!!VRz2&y-2<2:S^guard^;
!!VRz2&y-2>1:S^guards^;
!!IF&y-2>0:M^Found %Y-2 border %Z2^;
!!DO32165501/1/y-2/1&y-2>0:P9/y2/y3/y-2;
!!en:;


!?FU32165500;
;x1 border gate type
;x2 border gate subtype
;x3 active player
;x4 number of bg
!!UN:Ux1/x2/-1/1; store tent location in v1-v3
!!UN:Sv1/v2/v3/x3/2; show map
!!UN:Lv1/v2/v3/100; go to location
!!IF&x16<x4:Q1^Show next?^;
!!VRx16&-1:S999; exit

!?FU32165501;
;x1 border gate type
;x2 border gate subtype
;x3 active player
;x4 number of bg
!!UN:Ux1/x2/-1/1; store tent location in v1-v3
!!UN:Sv1/v2/v3/x3/2; show map
!!UN:Lv1/v2/v3/100; go to location
!!IF&x16<x4:Q1^Show next?^;
!!VRx16&-1:S999; exit


!?FU32165499;
;x1 tent type
;x2 tent subtype
;x3 active player
;x4 number of tents
!!UN:Ux1/x2/-1/1; store tent location in v1-v3
!!UN:Sv1/v2/v3/x3/2; show map
!!UN:Lv1/v2/v3/100; go to location
!!IF&x16<x4:Q1^Show next?^;
!!VRx16&-1:S999; exit

!?CM0;
;read what a seer hut or quest guard are asking
!!CM:P?y47/?y48/?y49;
!!OBy47/y48/y49:T?y50;
!!if|y50=83/y50=215:; seer huts
!!QUy47/y48/y49:Q?y3;
!!IF&y3=0:Q1/-1/-1/4^{Completed}^;
!!CM&y3=0:R0;
!!FU&y3=0:E;
  !!QUy47/y48/y49:M0/?z2 D?y99; check quest type
     !!if&y50=83:; read reward
     !!QUy47/y48/y49:R?y70/?y71/?y72;
     !!VRy71&y70>2/y70<5:%256; morale and luck
     !!VRz3&y70=0:S^No reward^;
     !!VRz3&y70=1:S^%Y71 experience points^;
     !!VRz3&y70=2:S^%Y71 spell points^;
     !!VRz3&y70=3:S^+%Y71 morale^;
     !!VRz3&y70=4:S^+%Y71 luck^;
     !!VRz4&y70=5/y71=0:S^wood^; !!VRz4&y70=5/y71=1:S^mercury^; !!VRz4&y70=5/y71=2:S^ore^;
     !!VRz4&y70=5/y71=3:S^sulfur^; !!VRz4&y70=5/y71=4:S^crystal^;  !!VRz4&y70=5/y71=5:S^gems^; !!VRz4&y70=5/y71=6:S^gold^;
     !!VRz3&y70=5:S^%Y72 %Z4^;
     !!VRz4&y70=6/y71=0:S^Attack^; !!VRz4&y70=6/y71=1:S^Defense^; !!VRz4&y70=6/y71=2:S^Spell Power^; !!VRz4&y70=6/y71=3:S^Knowledge^;
     !!VRy72&y70=6:&255; fix garbage values
     !!VRz3&y70=6:S^+%Y72 %Z4^; display primary skill reward
     !!UN&y70=7:N4/z5/y71; get secondary skill name in z5
     !!VRz4&y70=7/y72=1:S^Basic^;  !!VRz4&y70=7/y72=2:S^Advanced^; !!VRz4&y70=7/y72=3:S^Expert^;
     !!VRz3&y70=7:S^%Z4 %Z5^;
     !!UN&y70=8:N0/z3/y71; get artifact name
     !!UN&y70=9:N1/z3/y71; get spell name
     !!UN&y70=10:N3/z4/y71/1; get creature name
     !!VRy72&y70=10:&65535; fix garbage values
     !!VRz3&y70=10:S^%Y72 %Z4^;
     !!en:;
     !!VRz6&y99<0:S^{~Chartreuse}Deadline: none{~}^;
     !!VRy97:Sc0;
     !!VRy98&y99>-1:Sy99-y97;
     !!VRz6&y99>-1/y98>-1:S^{~Fuchsia}Deadline: %Y98 days left{~}^;
     !!VRz6&y99>-1/y98<0:S^{~Fuchsia}Expired{~}^;
!!IF&y3<>6/y50=83:Q1/-1/-1/4^{QUEST}: %Z2
{REWARD}: %Z3

%Z6^;
!!IF&y3<>6/y50=215:Q1/-1/-1/4^{QUEST}:
%Z2

%Z6^;
     !!if&y3=6:; creatures
     !!VRv9:C-1/-1; initialize v9-v10
     !!DO32165498/0/196/1:Py47/y48/y49;
     !!UN&v10>1:N3/z2/v9/1;
     !!UN&v10<2:N3/z2/v9/0;
     !!IF&y50=83:Q1/-1/-1/4^{QUEST}: Bring %V10 %Z2
{REWARD}: %Z3

%Z6^;
!!IF&y50=215:Q1/-1/-1/4^{QUEST}
Bring %V10 %Z2

%Z6^;
     !!en:;
!!CM:R0;
!!en:;
